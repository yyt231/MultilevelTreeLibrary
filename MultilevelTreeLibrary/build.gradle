plugins {
    id 'com.android.library'
    id 'maven-publish'
}
android {
    compileSdkVersion 34
    buildToolsVersion "34.0.0"
    namespace 'com.bigademo.treelist'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }


}
afterEvaluate {
    publishing {
        publications {
            // 这个mavenJava可以随便填，只是一个任务名字而已
            // MavenPublication必须有，这个是调用的任务类
            aarAndroid(MavenPublication) {
                // 这里头是artifacts的配置信息，不填会采用默认的
                groupId = 'com.bigademo'
                artifactId = 'MultilevelTreeLibrary'
                version = '2.0.0'

                artifact sourceJar
                // 必须有这个 否则不会上传AAR包  将bundleReleaseAar任务的单个输出文件作为发布的AAR文件。这样，该AAR文件就会成为 MavenPublication 的 artifact 并进行发布
                afterEvaluate { artifact(tasks.getByName("bundleReleaseAar")) }
            }

        }
    }
}
// 增加上传源码的task
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))
}
